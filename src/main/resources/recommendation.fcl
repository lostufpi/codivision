FUNCTION_BLOCK recommendation	// Definição do bloco

// Define variáveis de entrada
VAR_INPUT				
	FAMILIARIDADE : REAL;
	CORRELATO : REAL;
	TOTAL : REAL;
END_VAR

// Define variáveis de saída
VAR_OUTPUT				
	GRP : REAL; 
END_VAR

// Define as funções de pertinencia para a variável FAMILIARIDADE
FUZZIFY FAMILIARIDADE		
	TERM baixa := (0.0, 1) (0.3, 0);
	TERM  media := (0.25, 0) (0.5, 1) (0.75, 0);
	TERM alta := (0.7, 0)(1.0, 1);
END_FUZZIFY

// Define as funções de pertinencia para a variável CORRELATO
FUZZIFY CORRELATO			
	TERM baixo := (0.0, 1) (0.3, 0);
	TERM  medio := (0.25, 0) (0.5, 1) (0.75, 0);
	TERM alto := (0.7, 0)(1.0, 1);
END_FUZZIFY

// Define as funções de pertinencia para a variável TOTAL
FUZZIFY TOTAL			
	TERM baixo := (0.0, 1) (0.3, 0);
	TERM  medio := (0.25, 0) (0.5, 1) (0.75, 0);
	TERM alto := (0.7, 0)(1.0, 1);
END_FUZZIFY

// Define as funções de pertinencias para a variável de saída GRP
DEFUZZIFY GRP			
	TERM muitobaixo := (0.0, 1) (25.0, 0);
	TERM baixo := (15.0, 0) (30.0, 1) (45.0, 0);
	TERM medio := (35.0, 0) (50.0, 1) (65.0, 0);
	TERM alto := (55.0, 0) (70.0, 1) (85.0, 0);
	TERM muitoalto := (75.0, 0) (100.0, 1) ;
	METHOD : COG;		// Método de centro de gravidade
	DEFAULT := 0;		// Valor padrão (caso nenhuma regra for ativada)
END_DEFUZZIFY

// Definição das regras
RULEBLOCK ConjuntoRegras


	AND : MIN;	// Use 'min' for 'and'
	ACT : MIN;	// Use 'min' activation method
	ACCU : MAX;	// Use 'max' accumulation method
	
	//Para FAMILIARIDADE baixa
	RULE 1: IF FAMILIARIDADE IS baixa AND CORRELATO IS baixo AND TOTAL IS baixo THEN GRP IS muitobaixo;
	RULE 2: IF FAMILIARIDADE IS baixa AND CORRELATO IS baixo AND TOTAL IS medio THEN GRP IS muitobaixo;
	RULE 3: IF FAMILIARIDADE IS baixa AND CORRELATO IS baixo AND TOTAL IS alto THEN GRP IS baixo;
	
	RULE 4: IF FAMILIARIDADE IS baixa AND CORRELATO IS medio AND TOTAL IS baixo THEN GRP IS  baixo;
	RULE 5: IF FAMILIARIDADE IS baixa AND CORRELATO IS medio AND TOTAL IS medio THEN GRP IS baixo;
	RULE 6: IF FAMILIARIDADE IS baixa AND CORRELATO IS medio AND TOTAL IS alto THEN GRP IS medio;
	
	RULE 7: IF FAMILIARIDADE IS baixa AND CORRELATO IS alto AND TOTAL IS baixo THEN GRP IS  medio;
	RULE 8: IF FAMILIARIDADE IS baixa AND CORRELATO IS alto AND TOTAL IS medio THEN GRP IS medio;
	RULE 9: IF FAMILIARIDADE IS baixa AND CORRELATO IS alto AND TOTAL IS alto THEN GRP IS medio;
	
	//Para FAMILIARIDADE media
	RULE 10: IF FAMILIARIDADE IS media AND CORRELATO IS baixo AND TOTAL IS baixo THEN GRP IS muitobaixo;
	RULE 11: IF FAMILIARIDADE IS media AND CORRELATO IS baixo AND TOTAL IS medio THEN GRP IS baixo;
	RULE 12: IF FAMILIARIDADE IS media AND CORRELATO IS baixo AND TOTAL IS alto THEN GRP IS baixo;
	
	RULE 13: IF FAMILIARIDADE IS media AND CORRELATO IS medio AND TOTAL IS baixo THEN GRP IS  medio;
	RULE 14: IF FAMILIARIDADE IS media AND CORRELATO IS medio AND TOTAL IS medio THEN GRP IS medio;
	RULE 15: IF FAMILIARIDADE IS media AND CORRELATO IS medio AND TOTAL IS alto THEN GRP IS medio;
	
	RULE 16: IF FAMILIARIDADE IS media AND CORRELATO IS alto AND TOTAL IS baixo THEN GRP IS  alto;
	RULE 17: IF FAMILIARIDADE IS media AND CORRELATO IS alto AND TOTAL IS medio THEN GRP IS alto;
	RULE 18: IF FAMILIARIDADE IS media AND CORRELATO IS alto AND TOTAL IS alto THEN GRP IS alto;
	
	//Para FAMILIARIDADE alta
	RULE 19: IF FAMILIARIDADE IS alta AND CORRELATO IS baixo AND TOTAL IS baixo THEN GRP IS baixo;
	RULE 20: IF FAMILIARIDADE IS alta AND CORRELATO IS baixo AND TOTAL IS medio THEN GRP IS medio;
	RULE 21: IF FAMILIARIDADE IS alta AND CORRELATO IS baixo AND TOTAL IS alto THEN GRP IS medio;
	
	RULE 22: IF FAMILIARIDADE IS alta AND CORRELATO IS medio AND TOTAL IS baixo THEN GRP IS  medio;
	RULE 23: IF FAMILIARIDADE IS alta AND CORRELATO IS medio AND TOTAL IS medio THEN GRP IS alto;
	RULE 24: IF FAMILIARIDADE IS alta AND CORRELATO IS medio AND TOTAL IS alto THEN GRP IS alto;
	
	RULE 25: IF FAMILIARIDADE IS alta AND CORRELATO IS alto AND TOTAL IS baixo THEN GRP IS  alto;
	RULE 26: IF FAMILIARIDADE IS alta AND CORRELATO IS alto AND TOTAL IS medio THEN GRP IS muitoalto;
	RULE 27: IF FAMILIARIDADE IS alta AND CORRELATO IS alto AND TOTAL IS alto THEN GRP IS muitoalto;

END_RULEBLOCK

END_FUNCTION_BLOCK